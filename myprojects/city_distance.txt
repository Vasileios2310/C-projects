/**
 * Εισάγει μόνο το πάνω δεξια τρίγωνο του 5χ5 πίνακα και μόνο θετικούς αριθμούς
 */
ΔΙΑΔΙΚΑΣΙΑ INPUT(%CT)
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ : i, j
  ΠΡΑΓΜΑΤΙΚΕΣ : d
ΑΡΧΗ
  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ N-1
    ΓΙΑ j ΑΠΟ i+1 ΜΕΧΡΙ N
      ΕΠΑΝΑΛΑΒΕ
        ΤΥΠΩΣΕ "Δώσε την απόσταση μεταξύ ", i, "ης και ", j, "ης πόλης: "
        ΔΙΑΒΑΣΕ d
        ΑΝ d <= 0 ΤΟΤΕ
        ΤΥΠΩΣΕ "Η απόσταση πρέπει να είναι θετικός αριθμός"
        ΤΕΛΟΣ_ΑΝ
      ΜΕΧΡΙΣ_ΟΤΟΥ d > 0
      CT[i,j] := d
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
ΤΕΛΟΣ_ΔΙΑΔΙΚΑΣΙΑΣ

/**
 * Μηδενίζει τη διαγώνικο και αντιγράφει συμμετρικά το πάνω-δεξιά στοιχείο στο κάτω αριστερά στοιχείο
 */
ΔΙΑΔΙΚΑΣΙΑ FILL(%CT)
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ : i, j
ΑΡΧΗ
  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ N
    CT[i,i] := 0
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

  ΓΙΑ i ΑΠΟ 2 ΜΕΧΡΙ N
    ΓΙΑ j ΑΠΟ 1 ΜΕΧΡΙ i-1
      CT[i,j] := CT[j,i]
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
ΤΕΛΟΣ_ΔΙΑΔΙΚΑΣΙΑΣ

/**
 * Εκτύπωση ανα γραμμή με 2 δεκαδικά ψηφία
 */
ΔΙΑΔΙΚΑΣΙΑ OUTPUT(CT)
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ : i, j
ΑΡΧΗ
  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ N
    ΓΙΑ j ΑΠΟ 1 ΜΕΧΡΙ N
      ΤΥΠΩΣΕ CT[i,j] ΜΕ_2_ΔΕΚΑΔΙΚΑ, " "
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
    ΓΡΑΨΕ_ΝΕΑ_ΓΡΑΜΜΗ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
ΤΕΛΟΣ_ΔΙΑΔΙΚΑΣΙΑΣ

/**
 * Επιστρέφει τη μέγιστη απόσταση με παραμέτρους τις πόλεις
 */
ΣΥΝΑΡΤΗΣΗ FAR(CT, %CT1, %CT2) : ΠΡΑΓΜΑΤΙΚΗ
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ : i, j
  ΠΡΑΓΜΑΤΙΚΗ : maxD
ΑΡΧΗ
  maxD := CT[1,2]
  CT1 := 1
  CT2 := 2

  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ N-1
    ΓΙΑ j ΑΠΟ i+1 ΜΕΧΡΙ N
      ΑΝ CT[i,j] > maxD ΤΟΤΕ
        maxD := CT[i,j]
        CT1 := i
        CT2 := j
      ΤΕΛΟΣ_ΑΝ
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

  FAR := maxD
ΤΕΛΟΣ_ΣΥΝΑΡΤΗΣΗΣ

/**
 * Επιστρέφει την ελάχιστη απόσταση με παραμέτρους τις πόλεις
 */
ΣΥΝΑΡΤΗΣΗ NEAR(CT, %CT1, %CT2) : ΠΡΑΓΜΑΤΙΚΗ
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ : i, j
  ΠΡΑΓΜΑΤΙΚΗ : minD
ΑΡΧΗ
  minD := CT[1,2]
  CT1 := 1
  CT2 := 2

  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ N-1
    ΓΙΑ j ΑΠΟ i+1 ΜΕΧΡΙ N
      ΑΝ CT[i,j] < minD ΤΟΤΕ
        minD := CT[i,j]
        CT1 := i
        CT2 := j
      ΤΕΛΟΣ_ΑΝ
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

  NEAR := minD
ΤΕΛΟΣ_ΣΥΝΑΡΤΗΣΗΣ

/**
 * Επιστρέφει τη συνολική απόσταση ξεκινώντας απο την επιλογή του χρήστη (City)
 */
ΣΥΝΑΡΤΗΣΗ TRIP(CT, City) : ΠΡΑΓΜΑΤΙΚΗ
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ : j
  ΠΡΑΓΜΑΤΙΚΗ : sum
ΑΡΧΗ
  sum := 0
  ΓΙΑ j ΑΠΟ 1 ΜΕΧΡΙ N
    ΑΝ j <> City ΤΟΤΕ
      sum := sum + CT[City, j]
    ΤΕΛΟΣ_ΑΝ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

  TRIP := 2 * sum
ΤΕΛΟΣ_ΣΥΝΑΡΤΗΣΗΣ


ΠΡΟΓΡΑΜΜΑ CityDistances
ΣΤΑΘΕΡΕΣ
  N := 5
ΜΕΤΑΒΛΗΤΕΣ
  ΠΡΑΓΜΑΤΙΚΕΣ CT[1..N, 1..N]
  ΑΚΕΡΑΙΕΣ : start, a, b
  ΠΡΑΓΜΑΤΙΚΕΣ : dMax, dMin, totalTrip
ΑΡΧΗ
  ΚΑΛΕΣΕ INPUT(CT)
  ΚΑΛΕΣΕ FILL(CT)
  ΚΑΛΕΣΕ OUTPUT(CT)

  dMax := FAR(CT, a, b)
  ΤΥΠΩΣΕ "Μέγιστη απόσταση: ", dMax ΜΕ_2_ΔΕΚΑΔΙΚΑ, " μεταξύ ", a, "ης και ", b, "ης πόλης"

  dMin := NEAR(CT, a, b)
  ΤΥΠΩΣΕ "Ελάχιστη απόσταση: ", dMin ΜΕ_2_ΔΕΚΑΔΙΚΑ, " μεταξύ ", a, "ης και ", b, "ης πόλης"

  ΕΠΑΝΑΛΑΒΕ
    ΤΥΠΩΣΕ "Δώσε την πόλη-αφετηρία (1..", N, "): "
    ΔΙΑΒΑΣΕ start
    ΑΝ start < 1 Ή start > N ΤΟΤΕ
      ΤΥΠΩΣΕ "Η επιλογή σου πρέπει να είναι από 1 έως ", N
    ΤΕΛΟΣ_ΑΝ
  ΜΕΧΡΙΣ_ΟΤΟΥ start >= 1 ΚΑΙ start <= N

  totalTrip := TRIP(CT, start)
  ΤΥΠΩΣΕ "Το ταξίδι από την πόλη ", start, " προς όλες τις άλλες και επιστροφή είναι ",
        totalTrip ΜΕ_2_ΔΕΚΑΔΙΚΑ, " km"
ΤΕΛΟΣ_ΠΡΟΓΡΑΜΜΑΤΟΣ